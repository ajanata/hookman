;    HookMan - System Hook Manager for TI-83+ Series.
;    Copyright (C) 2007  Andy Janata
;
;    This program is free software; you can redistribute it and/or modify
;    it under the terms of the GNU General Public License as published by
;    the Free Software Foundation; either version 2 of the License, or
;    (at your option) any later version.
;
;    This program is distributed in the hope that it will be useful,
;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;    GNU General Public License for more details.
;
;    You should have received a copy of the GNU General Public License along
;    with this program; if not, write to the Free Software Foundation, Inc.,
;    51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
;
;	 You can contact the author via electronic mail at <ajanata@gmail.com>,
;	 or by writing to Andy Janata, 178 E. 5th St., Brewster, OH 44613 USA.


;	Routines for accessing the configuration appvar.
; 	These are only safe to use in "normal" parts of the application, not in hooks!
;	They use RAM at will.


;	AnyHooksInstalled: Checks to see if we have any hooks installed.
;	IN: None.
;	OUT: zero: no hooks installed, or config appvar not found (which should
;			never be the case anyway)
;	DESTROYS: All.
AnyHooksInstalled:
	call cfgToAppData
	ret z
	push ix
	xor a
	ld ix,appData
	or (ix+5)		; 0-4 are the signature
	or (ix+6)
	or (ix+7)
	pop ix
	ret
	
	
	
;	cfgToAppData: Copies the configuration appvar's signature and data to (appData).
;	IN: None.
;	OUT: zero: appvar not found or signature invalid.
;	DESTROYS: FIXME
cfgToAppData:
	ld hl,AV_HMConfig
	rst 20h
	bcall(_ChkFindSym)
	jr nc,+_
	xor a				; appvar not found, return with zero set
	ret
_:
	xor a
	cp b
	jr z,ctadRam			; appvar is in ram, just copy it
	
	; archived so flashtoram it
	ex de, hl			; I *think* not doing first ex de,hl, and putting 20 into hl, may work
	ld de,20			; skip over data valid flag, data length, symbol entry copy,
	call BHL_plus_DE_force	; name length, type byte, and name, to point to data
	ld a,b
	ld bc,8				; 5 byte signature + 3 data bytes
	ld de,appData
	bcall(_FlashToRAM)
	jr +_

ctadRam:
	ex de,hl
	inc hl
	inc hl
	ld de,appData
	ld bc,8
	ldir				; 5 byte signature + 3 data bytes
_:			
	; (appData) contains the start of the signature
	ld de,appData
	ld hl,Sig_HMConfig
	
	; 1
	ld a,(de)
	cp (hl)
	jr nz,+_
	inc de
	inc hl
	
	; 2
	ld a,(de)
	cp (hl)
	jr nz,+_
	inc de
	inc hl
	
	; 3
	ld a,(de)
	cp (hl)
	jr nz,+_
	inc de
	inc hl
	
	; 4
	ld a,(de)
	cp (hl)
	jr nz,+_
	inc de
	inc hl
	
	; 5
	ld a,(de)
	cp (hl)
	jr z,++_

_:
	; bad signature
	xor a
	ret
	
_:
	; good signature
	xor a
	inc a
	ret
	



